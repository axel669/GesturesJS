let isMobile = typeof orientation != "undefined"
    || navigator.userAgent.indexOf("Mobile") != -1
let handlers = Map*()

let touchVars = {}
let addHandler = (name, handler) => {
    if typeof handler == "function" {
        handler = handler()
    }

    handlers.set(name, handler)
    touchVars[name] = {}
}
let createEvent = (type, source) => {
    let newEvt = CustomEvent*(
        type
        {bubbles: true, cancelable: true}
    )

    for prop, value of source {
        newEvt[prop] = value
    }

    return newEvt
}
let copyTouchEvent = (touch) => {
    touch.clientX
    touch.clientY
    touch.pageX
    touch.pageY
    touch.screenX
    touch.screenY
    touch.identifier
    touch.target
    touch.sourceElement
    id: touch.identifier
    touch.timestamp
}
let copyForSynth = (touch) => {
    touch.clientX
    touch.clientY
    touch.pageX
    touch.pageY
    touch.screenX
    touch.screenY
    touch.identifier
    id: touch.identifier
}

let delay = (func) => setTimeout(func, 0)

let polarVector = (a, b) => {
    let dx = b.clientX - a.clientX
    let dy = b.clientY - a.clientY

    let angle = (
        Math.atan2(dy, dx)
        * (180 / Math.PI)
        + 450
    ) % 360
    let magnitude = Math.sqrt(dx ** 2 + dy ** 2)

    return {angle, magnitude}
}

let sharedVars = {}
let convertTouches = (source) => {
    let touches = [...source]

    for touch in touches {
        touch.timestamp = Date.now()
        touch.id = touch.identifier
    }

    return touches
}
let touchMapper = (name, reset = false) =>
    (touch) => {
        if (reset == true) {
            touchVars[name][touch.identifier] = {}
        }
        touchVars[name][touch.identifier] = {
            ...touchVars[name][touch.identifier]
            ...sharedVars[touch.identifier]
        }
        return {
            ...copyTouchEvent(touch)
            vars: touchVars[name][touch.identifier]
        }
    }
let handleTouchList = (list, individual, touches, evt) => {
    if list != void {
        list(touches, evt)
    }
    if individual == void {
        return
    }
    for touch in touches {
        individual(touch, evt)
    }
}
window.addEventListener(
    "touchstart"
    (evt) => {
        let touches = [...evt.changedTouches]
        for touch in touches {
            touch.timestamp = Date.now()
            sharedVars[touch.identifier] = {start: touch}
        }
        for [name, handler] in handlers {
            handleTouchList(
                handler.start
                handler.startItem
                touches.map(
                    touchMapper(name, true)
                )
                evt
            )
        }
    }
    false
)
window.addEventListener(
    "touchmove"
    (evt) => {
        let touches = [...evt.changedTouches]
        for touch in touches {
            let shared = sharedVars[touch.identifier]
            touch.timestamp = Date.now()
            shared.vector = polarVector(
                shared.start
                touch
            )
        }
        for [name, handler] in handlers {
            handleTouchList(
                handler.move
                handler.moveItem
                touches.map(
                    touchMapper(name)
                )
                evt
            )
        }
    }
)
window.addEventListener(
    "touchend"
    (evt) => {
        let touches = [...evt.changedTouches]
        for touch in touches {
            let shared = sharedVars[touch.identifier]
            touch.timestamp = Date.now()
            shared.vector = polarVector(
                shared.start
                touch
            )
        }
        for [name, handler] in handlers {
            handleTouchList(
                handler.end
                handler.endItem
                touches.map(
                    touchMapper(name)
                )
                evt
            )
        }
    }
)

if isMobile == false {
    console.log("GesturesJS will attach non-mobile listeners")
    let createSynthTouch = (mouseEvt) => {
        ...copyTouchEvent(mouseEvt)
        identifier: -10
        id: 10
        target: currentMouseTarget
        suorceElement: currentMouseTarget
    }

    let mut currentMouseTarget = null
    let mut mouseIsDown = false

    let dispatchSyntheticEvent = (evt, type) => {
        let changedTouches = [createSynthTouch(evt)]
        currentMouseTarget.dispatchEvent(
            createEvent(
                type
                {
                    changedTouches
                    touches: changedTouches
                    syntheticEvent: true
                }
            )
        )
    }

    window.addEventListener(
        "mousedown"
        (evt) => {
            if evt.button == 0 {
                mouseIsDown = true
                currentMouseTarget = evt.target
                dispatchSyntheticEvent(evt, "touchstart")
            }
        }
        true
    )
    window.addEventListener(
        "mousemove"
        (evt) => {
            if mouseIsDown == true {
                dispatchSyntheticEvent(evt, "touchmove")
            }
        }
        true
    )
    window.addEventListener(
        "mouseup"
        (evt) => {
            if evt.button == 0 && mouseIsDown == true {
                mouseIsDown = false
                dispatchSyntheticEvent(evt, "touchend")
                currentMouseTarget = null
            }
        }
        true
    )
}

let climbDOM = (start, func) => {
    let mut current = start
    while current != null && current != document.documentElement {
        func(current)
        current = current.parentNode
    }
}
addHandler(
    "active-touch"
    () => {
        let className = "gjs-touch-active"
        let activeTouches = WeakMap*()

        let inc = (elem) => {
            let count = activeTouches.get(elem) ?? 0
            activeTouches.set(elem, count + 1)
        }
        let dec = (elem) => {
            let newCount = activeTouches.get(elem) - 1
            activeTouches.set(elem, newCount)
            return newCount
        }

        return {
            startItem(touch) =>
                climbDOM(
                    touch.target
                    (node) => {
                        node.classList.add(className)
                        inc(node)
                    }
                )
            endItem(touch) =>
                climbDOM(
                    touch.target
                    (node) => {
                        if (dec(node) == 0) {
                            node.classList.remove(className)
                        }
                    }
                )
        }
    }
)
addHandler(
    "tap"
    () => {
        let className = "gjs-tap-active"
        let addClass = (node) => {
            node.classList.add(className)
        }
        let removeClass = (node) => {
            node.classList.remove(className)
        }

        return {
            startItem(touch) => {
                if touch.target.classList.contains(className) == false {
                    touch.vars.valid = true
                    touch.vars.active = true
                    climbDOM(touch.target, addClass)
                }
            }
            moveItem(touch) => {
                if touch.vars.active == true && touch.vars.vector.magnitude > 20 {
                    touch.vars.valid = false
                    climbDOM(touch.target, removeClass)
                }
            }
            endItem(touch) => {
                if touch.vars.active == true {
                    climbDOM(touch.target, removeClass)
                }
                let duration = touch.timestamp - touch.vars.start.timestamp
                if touch.vars.vector.magnitude > 20 || duration > 600 {
                    return
                }

                let synthEvent = createEvent("tap", copyForSynth(touch))
                delay(
                    () => {
                        if touch.target.dispatchEvent(synthEvent) == true {
                            touch.target.focus()
                        }
                    }
                )
            }
        }
    }
)
addHandler(
    "hold"
    () => {
        let timers = {}
        let schedule = (touch) => {
            timers[touch.id] = setTimeout(
                () => {
                    timers[touch.id] = null
                    touch.target.dispatchEvent(
                        createEvent("hold", copyForSynth(touch))
                    )
                }
                1500
            )
        }
        let clear = (touch) => {
            clearTimeout(timers[touch.id])
            timers[touch.id] = null
        }

        return {
            startItem(touch) => schedule(touch)
            moveItem(touch) => {
                if touch.vars.vector.magnitude > 20 {
                    clear(touch)
                }
            }
            endItem(touch) => clear(touch)
        }
    }
)

export default addHandler
